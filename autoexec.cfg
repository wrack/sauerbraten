bind "F10" [showgui Library]

curr_itemid = 99
curr_linkid = 999
opened_itemid = 99
curr_string = ""

closelist = [
	opened_itemid = 99 
	curr_linkid = 999
]

gotolibitem = [
	selectlibitem $arg1
	gotosel
]

gotoitemlink = [
	selectitemlink $arg1 $arg2
	gotosel
]

newgui Library [
	guistayopen [
		guilist [ 
			guibutton "" [libitem @curr_string] "wrack/goout_green"
			guifield curr_string 15 ""
			guibutton "" "" " "
			guibutton "" "applylib" "wrack/refresh_green"
		]
		
		guilist [ genlibitems ]
		guilist [ 
			guibutton "" [itemlink @curr_itemid] "wrack/goin_green"
			guibutton "" [dellibitem @curr_itemid] "wrack/trash_red"
			guibutton "" [setlibitemname @curr_itemid @curr_string] "wrack/text_cyan"
			guibutton "" [gotolibitem @curr_itemid] "wrack/write_green"
			guibutton "" [applylibitem @curr_itemid] "wrack/refresh_green"
		]
	]
]

genlibitems = [
	guilist [
		guibar
		loop i (libitemscnt) [
			cname = (getlibitemname $i)
			guilist [
				if (= $opened_itemid $i ) [
					guibutton "" "closelist" "wrack/tree_red"
				] [
					guibutton "" [opened_itemid = @i] "wrack/tree_green"
				]
				
				if (= $curr_itemid $i ) [
					guibutton "" [curr_itemid = 99] "wrack/ok_green"
					guitextbox $cname 15 1 0xFF0000
				] [
					guibutton "" [curr_itemid = @i] "wrack/red"
					guitextbox $cname 15 1 0xFF6400
				]
			]
			
			if (= $opened_itemid $i ) [
				genlinkitems $i $cname
			]
			
		]
		guibar
	]
]

genlinkitems = [
	loop i (linkscnt $arg1) [
		guilist [
			guibutton "" "" " "
			if (= $curr_linkid $i ) [
				guibutton "" [curr_linkid = 999] "wrack/ok_green"
				guitextbox (concatword $arg2 ":" $i) 17 1 0x006464
			] [
				guibutton "" [curr_linkid = @i] "wrack/red"
				guitextbox (concatword $arg2 ":" $i) 17 1 0x00FF64
			]
		]
	]
	guibar
	guilist [
		guibutton "" [gotoitemlink @arg1 @curr_linkid] "wrack/write_green"
		guibutton "" [applyitemlink @arg1 @curr_linkid] "wrack/refresh_green"
	]
	guibar
]

